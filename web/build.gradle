plugins {
    id "com.moowork.node" version "1.2.0"
    id "com.github.hierynomus.license" version"0.14.0"
}

def ciBuild = System.env['BuildRunner'] != null
def ciPublishBuild = ciBuild && (System.env['APPVEYOR_REPO_TAG'] == "true" || System.env['NPM_PUBLISH'] == "true")
def isWindows = org.gradle.internal.os.OperatingSystem.current().isWindows();
def e2eTestsEnabled = isWindows && System.env['BuildRunner'] != "MyGet" && System.env['PLEXUS_BUILD_SKIP_WEB_TESTS'] != 'true'
def ciFullBuild = ciBuild && e2eTestsEnabled
def npmPackageVersion = System.getenv("PLEXUS_BUILD_NPM_PACKAGE_VERSION") ?: (System.getenv("APPVEYOR_REPO_TAG_NAME") ?: System.getenv("PackageVersion")) 

node {
    version = '8.5.0'
    npmVersion = '5.7.1'    
    nodeModulesDir = projectDir
    download = true
}

license {
    header file("${rootDir}/LICENSE_HEADER")
    mapping {
        ts='JAVADOC_STYLE'
        proto='DOUBLESLASH_STYLE'
        interop='DOUBLESLASH_STYLE'
    }
    strictCheck true
}

task licenseFormatTS(type: com.hierynomus.gradle.license.tasks.LicenseFormat) {
    source = fileTree(dir: ".").exclude("**/node_modules/*")
            .include("**/*.ts").include("**/*.proto").include("**/*.interop")
}

tasks["license"].dependsOn licenseFormatTS
licenseFormat.dependsOn licenseFormatTS

task npmRunInstall(type: NpmTask, dependsOn: ':dsl:gen:cli:build') {
    args = ['install', '--no-package-lock'];
}

task preNpmBuild {
    doLast {

        println "NPM Package version: $npmPackageVersion"
	    println "CI Publish: $ciPublishBuild"
	    println "CI Full: $ciFullBuild"
	    println "CI Build: $ciBuild"
	    println "Integration tests: $e2eTestsEnabled"
        
        File packagesDir = file('packages');
        FileCollection collection = files { packagesDir.listFiles() }

        println "\nModules to build:"
        collection.collect { relativePath(it) }.sort().each { println it }
    }
}

task npmBuild(type: NpmTask, dependsOn: ['npmRunInstall', 'preNpmBuild']) {
    // https://github.com/srs/gradle-node-plugin/issues/128 
    environment = [
        'PLEXUS_BUILD_NPM_PACKAGE_VERSION': npmPackageVersion,
        'PLEXUS_CLI_SKIP_JRE_DOWNLOAD': 'true',
        'PLEXUS_CLI_SKIP_PROTOC_DOWNLOAD': 'true'
    ]
    args =  ciPublishBuild ? ['run', 'ci-publish'] :
            ciFullBuild ? ['run', 'ci-build-e2e'] : 
            ciBuild ? ['run', 'ci-build'] :
            e2eTestsEnabled ? ['run', 'build-e2e'] : ['run', 'build']
}

task buildTransport(dependsOn: npmBuild) {
    outputs.dir('.')
}

task build(dependsOn: buildTransport) {
}

build.dependsOn('license')
